//===== Hercules Script ======================================
//= Valentine Event Script
//===== By: ==================================================
//= Muad_Dib (Prometheus Project)
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= 08/28/05 : Ver. 1.01 Added a missing close; [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= 1.1 Cleaning. [Euphy]
//============================================================

// Stephen - Valentine Evento Vendedor de Chocolates
//============================================================
alberta,26,243,4	script	Stephen	1_M_MERCHANT,{
	mes "[Stephen]";
	mes "Adivinha o que eu tenho?";
	mes "Um Chocolate saboroso não é facilmente encontrado em Rune-Midgard...";
	next;
	mes "[Stephen]";
	mes "Chocolate!";
	mes "Isso mesmo, você não gosta de Chocolate?... Eu gosto!";
	mes "E você está com sorte, porque eu estou vendendo por apenas 5.000 zeny cada um!";
	next;
	if(select("Eu quero um pouco de Chocolate!:Não, obrigado.")==2) {
		mes "[Stephen]";
		mes "Você não quer um Chocolate?";
		mes "Eu estou te oferecendo! Não vá se arrepender!";
		mes "É melhor adquirir um agora... você não vai se deparar com um Chocolate como este nunca mais!";
		mes "Pense sobre isso e venha me visitar novamente algum dia.";
		close; }
	mes "[Stephen]";
	mes "Haha!";
	mes "Eu sabia!";
	mes "Mas eu não posso vender-lhe mais do que 5 por vez... Mas, se você realmente precisa de mais";
	mes "você pode voltar aqui.";
	mes "Então, quantos você quer?";
	next;
	input .@i;
	if (.@i <= 0) close;
	mes "[Stephen]";
	if (.@i > 5)  {
		mes "Sinto muito, mas não posso dar-lhe muitos.";
		close; }
	if (Zeny < .@i*5000) {
		mes "Sinto muito, mas parece que você não tem verba para comprá-los de mim.";
		close; }
	Zeny -= .@i*5000;
	getitem 558,.@i;
	mes "Aqui Está!";
	mes "Você pode dar isso a alguém como um presente, ou comer!";
	mes "Mmm... delícia de Chocolate...";
	mes "Visite-me qualquer hora!";
	close;
}

// Jainie -- Valentine Evento Fornece informações sobre o Evento dos Namorados
//============================================================
alberta,29,243,4	script	Jainie	1_M_INNKEEPER,{
	mes "[Jainie]";
	mes "Você sabia? Os Chocolates que meu namorado vende são de minha autoria!";
	mes "Eu mesma que produzo.";
	next;
	mes "[Jainie]";
	mes "Você sabe... Em alguns países, há uma tradição de presentar com Chocolates uma pessoa que você ama...";
	mes "Essa tradição se chama ^3355FFDia dos Namorados^000000.";
	next;
	mes "[Jainie]";
	mes "Então, eu dei a ele o meu delícioso Chocolate...";
	mes "E então ele me fez cozinhar muito mais...";
	mes "E agora ele vende a todos.";
	mes "Eu acho que ele realmente gostou";
	mes "Mas, também me sinto bem quando as pessoas compram algo que eu fiz.";
	next;
	mes "[Jainie]";
	mes "Seria ótimo se você compra-se alguns também...";
	mes "Eu estarei fazendo Chocolates por enquanto...";
	close;
}

// Carl Orleans -- Valentine Evento Fabricante de Chocolates
//============================================================
prt_castle,54,34,4	script	Carl Orleans	1_M_01,{
	mes "[Carl Orleans]";
	mes "Sim?";
	next;
	if(select("Eu quero um Chocolate caseiro...:Estou perdido, desculpe incomodá-lo.")==2) {
		mes "[Carl Orleans]";
		mes "Oh... bem, se você quiser que eu faça alguns de meus Chocolates...";
		mes "Você terá que me dar pelo menos ^0000FF 3 Chocolates^000000.";
		next;
		mes "[Carl Orleans]";
		mes "É isso mesmo, só ^0000FF 3 Chocolates^000000";
		mes "Traga-os para mim e você vai ter o que você veio procurar.";
		next;
		mes "[Carl Orleans]";
		mes "Volte sempre.";
		close; }
	mes "[Carl Orleans]";
	mes "Bem, talvez eu possa ajudar com seus desejos...";
	next;
	mes "[Carl Orleans]";
	if (countitem(558) < 3) {
		mes "Sinto muito, você não tem barras de Chocolate suficiente para fazer isso.";
		close; }
	delitem 558,3;
	mes "Você tem três pedaços de Chocolate puro, eu vejo.";
	mes "Dê-lhes para mim...";
	next;
	mes "[Carl Orleans]";
	mes "Ok, agora eu só irei criar meus Chocolates Especiais se você prometer usá-los sabiamente.";
	next;
	mes "[Carl Orleans]";
	mes "....Hmmmmmm.....";
	mes "Ok...";
	next;
	mes "[Carl Orleans]";
	getitem 559,1;
	mes "Aqui.";
	mes "Espero que dê para alguém especial, porque é um Chocolate especial.";
	mes "Como você sabe... que somente eu posso criá-los";
	next;
	mes "[Carl Orleans]";
	mes "Aproveite.";
	close;
}
